// Adding ScalaStyle support

apply plugin: 'scala'
apply plugin: 'project-report'

// Scala and Akka versions
def scalaMajor = "2.12"
def scalaMinor = "1"
def akkaVersion = "2.4.16"
def macroParadiseVersion = "2.1.0"

group 'io.github.vigoo'

repositories {
    mavenCentral()
}

configurations {
    scalaCompilerPlugin
}

dependencies {
    compile(
        [group: 'org.scala-lang', name: 'scala-compiler', version: "$scalaMajor.$scalaMinor"],
        [group: 'org.scala-lang', name: 'scala-library', version: "$scalaMajor.$scalaMinor"],
        [group: 'com.typesafe.akka', name: "akka-actor_$scalaMajor", version: akkaVersion]
    )

    scalaCompilerPlugin(
        [group: 'org.scalamacros', name: "paradise_$scalaMajor.$scalaMinor", version: "$macroParadiseVersion"]
    )

    testCompile(
        [group: 'junit', name: 'junit', version: '4.12'],
        [group: 'org.specs2', name: "specs2-core_$scalaMajor", version: '3.8.7'],
        [group: 'org.specs2', name: "specs2-junit_$scalaMajor", version: '3.8.7'],
    )
}

def paradiseJarName = "paradise_${scalaMajor}.${scalaMinor}-${macroParadiseVersion}.jar".toString()
tasks.withType(ScalaCompile) {
    configurations.scalaCompilerPlugin.forEach { jar ->
        if (paradiseJarName.equals(jar.name)) {
            scalaCompileOptions.additionalParameters = [ "-Xplugin:" + jar.absolutePath ]
        }
    }
            
	scalaCompileOptions.additionalParameters += ["-unchecked", "-deprecation", "-feature", "-Xfatal-warnings", "-encoding", "utf8"]
}

// specs2 console logging
test {
	testLogging.showStandardStreams = true
	systemProperty "specs2.commandline", "console"
}
